buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2')
        classpath('gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.5.1')
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.20.0'
}

apply from: 'gradle/utils.gradle'

allprojects {
    apply plugin: 'eclipse'

    group = 'io.github.scndry'
    version = '0.0.1-SNAPSHOT'
}

def bootProjects = utils.projectsOf 'server'

configure(bootProjects) {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.gorylenko.gradle-git-properties'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    springBoot {
        buildInfo {
            //noinspection GroovyAssignabilityCheck
            properties {
                additional = [
                    'spring-boot-version': springBootVersion
                ]
            }
        }
    }
}

configure(bootProjects) {
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'

    jacoco {
        toolVersion = '0.8.1'
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    sonarqube {
        //noinspection GroovyAssignabilityCheck
        properties {
            property 'sonar.projectName', 'spring-boot-blog'
            property 'sonar.projectKey', 'io.github.scndry.spring-boot-blog'
        }
    }
}
